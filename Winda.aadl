package winda
public
    with Base_Types;
    with Data_Model;
    with SEI;

    -- SYSTEM GLOWNY

    device Zasilacz_Przemyslowy
        features
            wyjscie_mocy : requires bus access Magistrala_Zasilania;
        properties
            SEI::GrossWeight => 5.0 kg;
    end Zasilacz_Przemyslowy;

    system System_Windy_3Pietra
        properties
            SEI::WeightLimit => 500.0 kg;
    end System_Windy_3Pietra;

    system implementation System_Windy_3Pietra.Impl
        subcomponents
            Kabina       : system Podsystem_Kabiny.Impl;
            Maszynownia  : system Podsystem_Maszynowni.Impl;
            
            Kabel_CAN       : bus Magistrala_CAN;
            Szyna_Zasilania : bus Magistrala_Zasilania;
            Glowne_Zasilanie: device Zasilacz_Przemyslowy;

        connections
            polaczenie_stanu  : port Kabina.wyjscie_stanu_master -> Maszynownia.wejscie_stanu_kabiny { SEI::BandWidthBudget => 5.0 KBytesps; };
            polaczenie_komend : port Maszynownia.wyjscie_komendy -> Kabina.wejscie_komendy_master { SEI::BandWidthBudget => 5.0 KBytesps; };

            fiz_kab_can : bus access Kabel_CAN <-> Kabina.port_can;
            fiz_masz_can: bus access Kabel_CAN <-> Maszynownia.port_can;

            fiz_zas_psu  : bus access Szyna_Zasilania <-> Glowne_Zasilanie.wyjscie_mocy;
            fiz_zas_kab  : bus access Szyna_Zasilania <-> Kabina.port_zasilania;
            fiz_zas_masz : bus access Szyna_Zasilania <-> Maszynownia.port_zasilania;

        flows
            Przeplyw_Sterowania_Ruchu : end to end flow
                Kabina.przeplyw_przyciskow -> polaczenie_stanu -> Maszynownia.ujscie_stanow;

        properties
            Actual_Connection_Binding => (reference (Kabel_CAN)) applies to polaczenie_stanu;
            Actual_Connection_Binding => (reference (Kabel_CAN)) applies to polaczenie_komend;

    end System_Windy_3Pietra.Impl;
    
    -- 1. DATA

    data Dane_Panelu
        properties 
            Data_Model::Data_Representation => Struct;
            Data_Size => 10 Bytes; 
    end Dane_Panelu;

    data implementation Dane_Panelu.Impl
        subcomponents
            Pietro_0      : data Base_Types::Boolean;
            Pietro_1      : data Base_Types::Boolean;
            Pietro_2      : data Base_Types::Boolean;
            Stop_Awaryjny : data Base_Types::Boolean;
            Otworz_Drzwi  : data Base_Types::Boolean;
            Zamknij_Drzwi : data Base_Types::Boolean;
    end Dane_Panelu.Impl;

    data Komenda_Kabiny
        properties 
            Data_Model::Data_Representation => Struct;
            Data_Size => 2 Bytes; 
    end Komenda_Kabiny;
    
    data implementation Komenda_Kabiny.Impl
        subcomponents
            Steruj_Drzwiami : data Base_Types::Boolean; 
            Wlacz_Swiatlo   : data Base_Types::Boolean;
    end Komenda_Kabiny.Impl;

    -- 2. BUS

    bus Magistrala_CAN
        properties
            SEI::BandWidthCapacity => 500.0 KBytesps;
            SEI::BandWidthBudget => 500.0 KBytesps;
            Latency => 1 ms .. 2 ms;
    end Magistrala_CAN;

    bus Magistrala_Systemowa
        properties
            SEI::BandWidthCapacity => 100.0 MBytesps;
            SEI::BandWidthBudget => 100.0 MBytesps;
            Latency => 1 ns .. 5 ns;
    end Magistrala_Systemowa;

    bus Magistrala_Video
        properties
            SEI::BandWidthCapacity => 100.0 MBytesps;
            SEI::BandWidthBudget => 100.0 MBytesps;
            Latency => 10 ms .. 20 ms;
    end Magistrala_Video;

    bus Magistrala_Zasilania
        properties
            SEI::PowerCapacity => 7000.0 w;
            SEI::BandWidthCapacity => 1.0 bitsps;
            SEI::BandWidthBudget => 1.0 bitsps;
    end Magistrala_Zasilania;

    bus Magistrala_Lokalna
        properties
            SEI::BandWidthCapacity => 100.0 MBytesps;
            SEI::BandWidthBudget => 100.0 MBytesps;
    end Magistrala_Lokalna;

    -- 3. PAMIECI

    memory Typ_Flash
        features
            dostep : requires bus access Magistrala_Systemowa;
        properties 
            Memory_Size => 1024 KByte;
    end Typ_Flash;

    memory Typ_RAM
        features
            dostep : requires bus access Magistrala_Systemowa;
        properties 
            Memory_Size => 512 KByte;
    end Typ_RAM;

    memory Pamiec_Sterownika
        features
            dostep_sys: requires bus access Magistrala_Systemowa;
    end Pamiec_Sterownika;
    
    memory implementation Pamiec_Sterownika.Zlozona
        subcomponents
            Prog_Flash : memory Typ_Flash;
            Dane_RAM   : memory Typ_RAM;
        connections
            c_flash: bus access dostep_sys <-> Prog_Flash.dostep;
            c_ram:   bus access dostep_sys <-> Dane_RAM.dostep;
    end Pamiec_Sterownika.Zlozona;

    memory Pamiec_Mala
        features
            dostep_lokalny : requires bus access Magistrala_Lokalna;
    end Pamiec_Mala;

    -- PODSYSTEM 1: KABINA WINDY 
    
    processor Procesor_Kabiny
        features
            lacze_can       : requires bus access Magistrala_CAN;
            lacze_zasilania : requires bus access Magistrala_Zasilania;
            lacze_pamieci   : requires bus access Magistrala_Lokalna;
        properties
            SEI::MIPSCapacity => 60.0 mips;
            SEI::GrossWeight => 0.1 kg;
    end Procesor_Kabiny;

    device Mechanizm_Drzwi
        features
            komenda : in data port Base_Types::Boolean;
            zasilanie : requires bus access Magistrala_Zasilania;
            dostep_dane : requires bus access Magistrala_Lokalna;
        flows
            ujscie_drzwi : flow sink komenda;
        properties
            Latency => 100 ms .. 500 ms;
            SEI::PowerBudget => 200.0 w applies to zasilanie;
    end Mechanizm_Drzwi;

    device Oswietlenie_Kabiny
        features
            wlacz : in data port Base_Types::Boolean;
            zasilanie : requires bus access Magistrala_Zasilania;
            dostep_dane : requires bus access Magistrala_Lokalna;
    end Oswietlenie_Kabiny;

    device Kamera_Monitoringu
        features
            obraz_out : out data port Base_Types::Integer;
            zasilanie : requires bus access Magistrala_Zasilania;
        properties
            SEI::PowerBudget => 5.0 w applies to zasilanie;
    end Kamera_Monitoringu;

    device Panel_Przyciskow
        features
            stan_przyciskow : out data port Dane_Panelu.Impl;
            zasilanie       : requires bus access Magistrala_Zasilania;
            dostep_dane : requires bus access Magistrala_Lokalna;
        flows
            zrodlo_klikniec : flow source stan_przyciskow;
    end Panel_Przyciskow;

    thread Watek_Obslugi_Kabiny
        features
            komenda_in  : in data port Komenda_Kabiny.Impl;
            przyciski_in: in data port Dane_Panelu.Impl;
            drzwi_out   : out data port Base_Types::Boolean;
            swiatlo_out : out data port Base_Types::Boolean;
            status_out  : out data port Dane_Panelu.Impl;
        flows
            sciezka_drzwi : flow path komenda_in -> drzwi_out;
            sciezka_stan  : flow path przyciski_in -> status_out;
        properties
            Dispatch_Protocol => Periodic;
            Period => 50 ms;
            Compute_Execution_Time => 2 ms .. 5 ms;
    end Watek_Obslugi_Kabiny;

    process Proces_Kabiny
        features
            komenda_in  : in data port Komenda_Kabiny.Impl;
            przyciski_in: in data port Dane_Panelu.Impl;
            drzwi_out   : out data port Base_Types::Boolean;
            swiatlo_out : out data port Base_Types::Boolean;
            status_out  : out data port Dane_Panelu.Impl;
        flows
            sciezka_proc_drzwi : flow path komenda_in -> drzwi_out;
            sciezka_proc_stan  : flow path przyciski_in -> status_out;
        properties
            SEI::MIPSBudget => 15.0 mips;
    end Proces_Kabiny;

    process implementation Proces_Kabiny.Impl
        subcomponents
            watek : thread Watek_Obslugi_Kabiny;
        connections
            c1: port komenda_in -> watek.komenda_in { SEI::BandWidthBudget => 5.0 KBytesps; };
            c2: port przyciski_in -> watek.przyciski_in { SEI::BandWidthBudget => 5.0 KBytesps; };
            c3: port watek.drzwi_out -> drzwi_out { SEI::BandWidthBudget => 1.0 KBytesps; };
            c4: port watek.swiatlo_out -> swiatlo_out { SEI::BandWidthBudget => 1.0 KBytesps; };
            c5: port watek.status_out -> status_out { SEI::BandWidthBudget => 5.0 KBytesps; };
        flows
            sciezka_proc_drzwi : flow path komenda_in -> c1 -> watek.sciezka_drzwi -> c3 -> drzwi_out;
            sciezka_proc_stan  : flow path przyciski_in -> c2 -> watek.sciezka_stan -> c5 -> status_out;
    end Proces_Kabiny.Impl;

    system Podsystem_Kabiny
        features
            wejscie_komendy_master : in data port Komenda_Kabiny.Impl;
            wyjscie_stanu_master   : out data port Dane_Panelu.Impl;
            port_can       : requires bus access Magistrala_CAN;
            port_zasilania : requires bus access Magistrala_Zasilania;
        flows
            przeplyw_przyciskow : flow source wyjscie_stanu_master;
    end Podsystem_Kabiny;

    system implementation Podsystem_Kabiny.Impl
        subcomponents
            cpu        : processor Procesor_Kabiny {
                SEI::PowerBudget => 2.0 w applies to lacze_zasilania;
            };
            
            pamiec     : memory Pamiec_Mala;
            szyna_lok  : bus Magistrala_Lokalna;
            
            aplikacja  : process Proces_Kabiny.Impl;
            
            drzwi      : device Mechanizm_Drzwi;
            swiatlo    : device Oswietlenie_Kabiny {
                SEI::PowerBudget => 20.0 w applies to zasilanie;
            };
            kamera     : device Kamera_Monitoringu;
            panel      : device Panel_Przyciskow {
                SEI::PowerBudget => 1.0 w applies to zasilanie;
            };
            
        connections
            c_pan: port panel.stan_przyciskow -> aplikacja.przyciski_in { SEI::BandWidthBudget => 5.0 KBytesps; };
            c_drz: port aplikacja.drzwi_out -> drzwi.komenda { SEI::BandWidthBudget => 1.0 KBytesps; };
            c_sw : port aplikacja.swiatlo_out -> swiatlo.wlacz { SEI::BandWidthBudget => 1.0 KBytesps; };
            
            c_in : port wejscie_komendy_master -> aplikacja.komenda_in { SEI::BandWidthBudget => 5.0 KBytesps; };
            c_out: port aplikacja.status_out -> wyjscie_stanu_master { SEI::BandWidthBudget => 5.0 KBytesps; };
            
            fiz_cpu_can  : bus access port_can <-> cpu.lacze_can;
            fiz_cpu_mem  : bus access szyna_lok <-> cpu.lacze_pamieci;
            fiz_mem      : bus access szyna_lok <-> pamiec.dostep_lokalny;
            
            zas_cpu      : bus access port_zasilania <-> cpu.lacze_zasilania;
            zas_drzwi    : bus access port_zasilania <-> drzwi.zasilanie;
            zas_swiatlo  : bus access port_zasilania <-> swiatlo.zasilanie;
            zas_kamera   : bus access port_zasilania <-> kamera.zasilanie;
            zas_panel    : bus access port_zasilania <-> panel.zasilanie;

            dane_drzwi   : bus access szyna_lok <-> drzwi.dostep_dane;
            dane_swiatlo : bus access szyna_lok <-> swiatlo.dostep_dane;
            dane_panel   : bus access szyna_lok <-> panel.dostep_dane;
            
        flows
             przeplyw_przyciskow : flow source panel.zrodlo_klikniec -> c_pan -> aplikacja.sciezka_proc_stan -> c_out -> wyjscie_stanu_master;
             
        properties
            Actual_Processor_Binding => (reference (cpu)) applies to aplikacja;
            Actual_Memory_Binding => (reference (pamiec)) applies to aplikacja;
           
            Actual_Connection_Binding => (reference (szyna_lok)) applies to c_pan;
            Actual_Connection_Binding => (reference (szyna_lok)) applies to c_drz;
            Actual_Connection_Binding => (reference (szyna_lok)) applies to c_sw;
    end Podsystem_Kabiny.Impl;

    -- PODSYSTEM 2: STEROWANIE 

    processor Procesor_Glowny
        features
            lacze_sys       : requires bus access Magistrala_Systemowa;
            lacze_can       : requires bus access Magistrala_CAN;
            lacze_zasilania : requires bus access Magistrala_Zasilania;
        properties
            SEI::MIPSCapacity => 200.0 mips;
            Clock_Period => 10000 ps;
    end Procesor_Glowny;

    device Silnik_Wciagarki
        features
            predkosc_pwm : in data port Base_Types::Integer;
            zasilanie    : requires bus access Magistrala_Zasilania;
            dostep_dane  : requires bus access Magistrala_Systemowa;
        flows
            ujscie_silnika : flow sink predkosc_pwm;
        properties
            SEI::PowerBudget => 3000.0 w applies to zasilanie;
            Latency => 50 ms .. 100 ms;
    end Silnik_Wciagarki;
    
    device Wyswietlacz_Pietra
        features
            nr_pietra : in data port Base_Types::Integer;
            zasilanie : requires bus access Magistrala_Zasilania;
            dostep_dane  : requires bus access Magistrala_Systemowa;
    end Wyswietlacz_Pietra;

    thread Watek_Logiki_Windy
        features
            panel_in     : in data port Dane_Panelu.Impl;
            
            silnik_out   : out data port Base_Types::Integer;
            pietro_out   : out data port Base_Types::Integer;
            
            komenda_kabiny_out : out data port Komenda_Kabiny.Impl;
        
        flows
            sciezka_silnik : flow path panel_in -> silnik_out;
            sciezka_kabina : flow source komenda_kabiny_out;

        properties
            Dispatch_Protocol => Periodic;
            Period => 20 ms;
            Compute_Execution_Time => 5 ms .. 10 ms;
    end Watek_Logiki_Windy;

    process Proces_Sterowania
        features
            panel_in     : in data port Dane_Panelu.Impl;
            silnik_out   : out data port Base_Types::Integer;
            pietro_out   : out data port Base_Types::Integer;
            komenda_out  : out data port Komenda_Kabiny.Impl;
        flows
            sciezka_silnik : flow path panel_in -> silnik_out;
            sciezka_kabina : flow source komenda_out;
        properties
            SEI::MIPSBudget => 100.0 mips;
    end Proces_Sterowania;

    process implementation Proces_Sterowania.Impl
        subcomponents
            watek : thread Watek_Logiki_Windy;
        connections
            c1: port panel_in -> watek.panel_in { SEI::BandWidthBudget => 5.0 KBytesps; };
            c_sil: port watek.silnik_out -> silnik_out { SEI::BandWidthBudget => 1.0 KBytesps; };
            c_pie: port watek.pietro_out -> pietro_out { SEI::BandWidthBudget => 1.0 KBytesps; };
            c_kom: port watek.komenda_kabiny_out -> komenda_out { SEI::BandWidthBudget => 5.0 KBytesps; };
        flows
            sciezka_silnik : flow path panel_in -> c1 -> watek.sciezka_silnik -> c_sil -> silnik_out;
            sciezka_kabina : flow source watek.sciezka_kabina -> c_kom -> komenda_out;
    end Proces_Sterowania.Impl;

    system Podsystem_Maszynowni
        features
            wejscie_stanu_kabiny : in data port Dane_Panelu.Impl;
            wyjscie_komendy      : out data port Komenda_Kabiny.Impl;
            
            port_can       : requires bus access Magistrala_CAN;
            port_zasilania : requires bus access Magistrala_Zasilania;
        flows
            sciezka_sterowania : flow source wyjscie_komendy;
            ujscie_stanow : flow sink wejscie_stanu_kabiny;
    end Podsystem_Maszynowni;

    system implementation Podsystem_Maszynowni.Impl
        subcomponents
            cpu        : processor Procesor_Glowny {
                SEI::PowerBudget => 5.0 w applies to lacze_zasilania;
            };
            
            pamiec     : memory Pamiec_Sterownika.Zlozona;
            szyna_sys  : bus Magistrala_Systemowa;
            
            aplikacja  : process Proces_Sterowania.Impl;
            
            silnik     : device Silnik_Wciagarki;
            lcd_pietra : device Wyswietlacz_Pietra {
                SEI::PowerBudget => 10.0 w applies to zasilanie;
            };

        connections
            l_stan : port wejscie_stanu_kabiny -> aplikacja.panel_in { SEI::BandWidthBudget => 5.0 KBytesps; };
            l_kom  : port aplikacja.komenda_out -> wyjscie_komendy { SEI::BandWidthBudget => 5.0 KBytesps; };
            
            l_sil  : port aplikacja.silnik_out -> silnik.predkosc_pwm { SEI::BandWidthBudget => 1.0 KBytesps; };
            l_lcd  : port aplikacja.pietro_out -> lcd_pietra.nr_pietra { SEI::BandWidthBudget => 1.0 KBytesps; };

            fiz_cpu_sys : bus access szyna_sys <-> cpu.lacze_sys;
            fiz_mem_sys : bus access szyna_sys <-> pamiec.dostep_sys;
            fiz_cpu_can : bus access port_can <-> cpu.lacze_can;

            zas_cpu : bus access port_zasilania <-> cpu.lacze_zasilania;
            zas_sil : bus access port_zasilania <-> silnik.zasilanie;
            zas_lcd : bus access port_zasilania <-> lcd_pietra.zasilanie;
            
            dane_silnik : bus access szyna_sys <-> silnik.dostep_dane;
            dane_lcd    : bus access szyna_sys <-> lcd_pietra.dostep_dane;

        flows
            sciezka_sterowania : flow source aplikacja.sciezka_kabina -> l_kom -> wyjscie_komendy;
            ujscie_stanow : flow sink wejscie_stanu_kabiny -> l_stan -> aplikacja.sciezka_silnik -> l_sil -> silnik.ujscie_silnika;

        properties
            Actual_Processor_Binding => (reference (cpu)) applies to aplikacja;
            Actual_Memory_Binding => (reference (pamiec)) applies to aplikacja;
            
            Actual_Connection_Binding => (reference (szyna_sys)) applies to l_sil;
            Actual_Connection_Binding => (reference (szyna_sys)) applies to l_lcd;

    end Podsystem_Maszynowni.Impl;


end winda;
